plugins {
    id 'java'
    id 'net.kyori.blossom' version '1.2.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group "$projectGroup"
version "$projectVersion"

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://reposilite.koboo.eu/releases'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.13.1-R0.1-SNAPSHOT'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly "org.spigotmc:plugin-annotations:$pluginAnnotationsVersion"
    annotationProcessor "org.spigotmc:plugin-annotations:$pluginAnnotationsVersion"

    implementation 'eu.koboo:yaml-config:1.3.3'

    //https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

java {
    targetCompatibility = JavaVersion.VERSION_11
    sourceCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.fork = true
    options.incremental = true
    options.encoding = 'UTF-8'
    options.release.set(11)
}

blossom {
    replaceToken('PROJECT_NAME', project.name)
    replaceToken('PROJECT_VERSION', project.version)
    replaceToken('PROJECT_GROUP', projectGroup)
    replaceToken('PROJECT_DESCRIPTION', projectDescription)
    replaceToken('PROJECT_WEBSITE', projectWebsite)
}

shadowJar {
    relocate 'org.bstats', 'eu.koboo.elevator.libs.bstats'
    relocate 'eu.koboo.yaml', 'eu.koboo.elevator.libs.yaml'
}
